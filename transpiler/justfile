set shell := ["bash", "-o", "pipefail", "-cu"]

# Configuration
source := "../src/parable.py"
tests := "../tests"
dist := "../dist/go"
backends := "c csharp dart go java javascript lua php perl python ruby typescript"
testable := "dart csharp go java javascript lua perl php python ruby typescript"

# Transpile source to stdout
emit backend:
    uv run python -m src.tongues --target {{backend}} < {{source}}

# Transpile source and check syntax/compilation
prep backend:
    #!/usr/bin/env bash
    set -euo pipefail
    just style
    case "{{backend}}" in
        c)
            just -f ../dist/c/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        csharp)
            just -f ../dist/csharp/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        dart)
            just -f ../dist/dart/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        go)
            just -f ../dist/go/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        java)
            just -f ../dist/java/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        javascript)
            just -f ../dist/javascript/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        lua)
            just -f ../dist/lua/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        python)
            just -f ../dist/python/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        typescript)
            just -f ../dist/typescript/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        perl)
            just -f ../dist/perl/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        php)
            just -f ../dist/php/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        ruby)
            just -f ../dist/ruby/justfile build "$(pwd)/{{source}}" "$(pwd)"
            ;;
        *)
            echo "Unknown backend: {{backend}}"
            exit 1
            ;;
    esac

# Run full parser test suite (4574 tests)
test backend:
    #!/usr/bin/env bash
    set -euo pipefail
    # Convert relative paths to absolute for subprocesses
    tests_abs=$(cd "$(dirname "{{tests}}")" && pwd)/$(basename "{{tests}}")
    case "{{backend}}" in
        c)
            just -f ../dist/c/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        dart)
            just -f ../dist/dart/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        go)
            just -f ../dist/go/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        java)
            just -f ../dist/java/justfile check "$(pwd)/../src/parable.py" "$(pwd)" "$tests_abs"
            ;;
        javascript)
            just -f ../dist/javascript/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        lua)
            just -f ../dist/lua/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        python)
            just -f ../dist/python/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        typescript)
            just -f ../dist/typescript/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        perl)
            just -f ../dist/perl/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        php)
            just -f ../dist/php/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        ruby)
            just -f ../dist/ruby/justfile check "$(pwd)/{{source}}" "$(pwd)" "$tests_abs"
            ;;
        *)
            echo "No test runner for backend: {{backend}}"
            echo "Testable backends: {{testable}}"
            exit 1
            ;;
    esac

# Check all backends compile
prep-all:
    #!/usr/bin/env bash
    set -euo pipefail
    for backend in {{backends}}; do
        echo "=== Checking $backend ==="
        just prep "$backend" || echo "FAILED: $backend"
    done

# Run test suite on all testable backends
test-all:
    #!/usr/bin/env bash
    set -euo pipefail
    for backend in {{testable}}; do
        echo "=== Testing $backend ==="
        just test "$backend"
    done

# Show compilation errors for debugging
errors backend:
    @uv run python -m src.tongues --target {{backend}} < {{source}} > /tmp/transpile-out 2>&1 || true
    @echo "=== Transpiler output (first 20 lines) ==="
    @head -20 /tmp/transpile-out || true
    @echo "=== Compilation errors ==="
    @just prep {{backend}} 2>&1 | head -50 || true

# Verify all transpiler source is subset-compliant
style:
    #!/usr/bin/env bash
    set -euo pipefail
    failed=0
    for f in $(fd -e py . src) tests/run_tests.py; do
        if ! uv run python -m src.tongues --verify < "$f" 2>/dev/null; then
            echo "FAIL: $f"
            uv run python -m src.tongues --verify < "$f" 2>&1 | head -5
            failed=1
        fi
    done
    exit $failed

# Run phase tests (subset/names verification)
test-phases:
    python3 tests/run_testsw.py tests/phases/

# Run codegen tests
test-codegen:
    python3 tests/run_codegen_testsw.py tests/codegen/

# Run all transpiler tests
test-transpiler: test-phases test-codegen
